import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask

plugins {
    id "org.springframework.boot" version "${springBootVersion}"
    id "org.cyclonedx.bom" version "${cyclonedxBomVersion}"
    id "io.spring.dependency-management" version "${springDependencyManangementVersion}"
    id "org.owasp.dependencycheck" version "${owaspDependencyCheckVersion}"
    id "com.github.ben-manes.versions" version "${versionsPlugin}"
}

repositories {
    mavenCentral()
}

apply plugin: "java"
apply plugin: "jacoco"

group = "rocks.inspectit.ocelot"

java {
    sourceCompatibility = 21
    targetCompatibility = 21
}

if (!project.hasProperty("buildVersion") || project.getProperty("buildVersion").empty) {
    ext.buildVersion = "SNAPSHOT"
}

version = "$buildVersion"

tasks.register('downloadBoomerangjs') {
    logger.info("Downloading Boomerangjs version {}.", boomerangVersion)
    ext.dest = layout.buildDirectory.file("boomerang-source-${boomerangVersion}.tgz").get().asFile
    outputs.files(ext.dest)
    doLast {
        def f = ext.dest
        new URI("https://registry.npmjs.org/boomerangjs/-/boomerangjs-${boomerangVersion}.tgz").toURL()
                .withInputStream { i -> f.withOutputStream { it << i } }
    }
}

tasks.register('deleteBoomerangjs', Delete) {
    delete layout.buildDirectory.file("boomerangjs-${boomerangVersion}")
}

tasks.register ('downloadAndExtractBoomerang', Copy) {
    dependsOn deleteBoomerangjs, downloadBoomerangjs
    from tarTree(downloadBoomerangjs.dest)
    into layout.buildDirectory.dir("boomerangjs-${boomerangVersion}")
    filter { line -> line.replaceAll("%boomerang_version%", "${boomerangVersion}") }
}

tasks.register('generateVersionFile') {
    ext.versionFile = layout.buildDirectory.file("eum-version.info").get().asFile
    doLast {
        def currentDate = new Date().toString()
        ext.versionFile.withWriter("UTF-8") { writer ->
            writer << "$version\n$currentDate\n$boomerangVersion\n$openTelemetryVersion"
        }
    }
}

tasks.register('downloadOpenTelemetryPlugin') {
    ext.dest = layout.buildDirectory.file("boomerangjs-${boomerangVersion}/package/plugins/boomerang-opentelemetry.js").get().asFile
    outputs.files(ext.dest)
    doLast {
        def f = ext.dest
        new URI("https://github.com/NovatecConsulting/boomerang-opentelemetry-plugin/releases/download/$boomerangOpenTelemetryPluginVersion/boomerang-opentelemetry.js").toURL()
                .withInputStream { i -> f.withOutputStream { it << i } }
    }
}

bootJar {
    dependsOn generateVersionFile
    dependsOn downloadAndExtractBoomerang
    dependsOn downloadOpenTelemetryPlugin

    archiveVersion = "${buildVersion}"

    base {
        archivesName = "inspectit-ocelot-eum-server"
    }

    manifest {
        attributes "Start-Class": "rocks.inspectit.ocelot.eum.server.EUMServerApplication"
    }

    // include version file
    from generateVersionFile.versionFile

    // include boomerang
    from(layout.buildDirectory.dir("boomerangjs-${boomerangVersion}/package")) {
        include "plugins/*.js"
        include "boomerang.js"
        into "static/boomerang"
    }

    // include boomerang opentelemetry
    from(layout.buildDirectory) {
        include "boomerang-opentelemetry.js"
        into "static/boomerang"
    }
}

cyclonedxBom {
    includeConfigs = ["runtimeClasspath"]
    schemaVersion = "1.4"
    projectType = "application"
    outputName = "bom"
    outputFormat = "all"
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = "full"
    }
}

dependencies {
    implementation(
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-validation",
            "org.springframework.boot:spring-boot-starter-security",

            "org.yaml:snakeyaml:${snakeYamlVersion}",
            // Has to be included, but is transitive to spring
            "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml",

            "io.grpc:grpc-context:${grpcVersion}",

            platform("io.opentelemetry:opentelemetry-bom-alpha:${openTelemetryAlphaVersion}"),
            platform("io.opentelemetry:opentelemetry-bom:${openTelemetryVersion}"),
            "io.opentelemetry:opentelemetry-sdk:${openTelemetryVersion}",
            "io.opentelemetry:opentelemetry-api:${openTelemetryVersion}",
            "io.opentelemetry:opentelemetry-sdk-metrics:${openTelemetryVersion}",
            "io.opentelemetry:opentelemetry-exporter-otlp:${openTelemetryVersion}",
            "io.opentelemetry:opentelemetry-exporter-prometheus:${openTelemetryAlphaVersion}",
            "io.opentelemetry.semconv:opentelemetry-semconv:${openTelemetrySemConvVersion}",
            "io.opentelemetry.proto:opentelemetry-proto:${openTelemetryProtoVersion}",

            "com.google.protobuf:protobuf-java:${protobufVersion}",
            "com.google.protobuf:protobuf-java-util:${protobufVersion}",

            "com.google.guava:guava:${guavaVersion}",
            "com.maxmind.geoip2:geoip2:${geoip2Version}",
            "commons-net:commons-net:${commonsNetVersion}",
            "org.apache.commons:commons-lang3:${commonsLang3Version}",
            "org.apache.commons:commons-math3:${commonsMath3Version}",
            "commons-io:commons-io:${commonsIoVersion}"
    )

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation(
            "org.springframework.boot:spring-boot-starter-test",
            "org.apache.httpcomponents:httpclient:${httpClientVersion}",

            // ServerExtension
            "com.linecorp.armeria:armeria-junit5:${armeriaVersion}",
            "com.linecorp.armeria:armeria-grpc-protocol:${armeriaVersion}",

            // for docker test containers
            "org.testcontainers:testcontainers:${testContainersVersion}",
            "org.testcontainers:junit-jupiter:${testContainersVersion}"
    )

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

// Copy the jar into the docker folder without version suffix
tasks.register('copyServerJar', Copy) {
    dependsOn bootJar
    from(layout.buildDirectory.file("libs/inspectit- lot-eum-server-${version}.jar"))
    into(layout.projectDirectory.dir("docker"))
    rename("inspectit-ocelot-eum-server-${version}\\.jar",
            "inspectit-ocelot-eum-server.jar")
}

// Normally we use the dependency-check GitHub action to check dependencies
// We keep this configuration, if you want to run the dependency-check locally
dependencyCheck {
    failBuildOnCVSS = 6
    suppressionFile = "dependencyCheckSuppression.xml"
    analyzers.assemblyEnabled = false
    analyzers.ossIndex.enabled = true

    nvd.apiKey = System.getenv("NVD_API_TOKEN")
    nvd.delay = 10000
}

def isNonStable = { String candidate ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE'].any { it -> candidate.toUpperCase().contains(it) }
    def versionRegex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(candidate ==~ versionRegex)
}

def isNotSameMajorMinor = { String current, String candidate, boolean matchMinor ->
    if(current == candidate) return false

    def firstDot = current.indexOf('.')
    def secondDot = current.indexOf('.', firstDot + 1)
    def major = current.substring(0, firstDot)
    def minor = current.substring(firstDot + 1, secondDot)
    def majorRegex = /^$major\..*/
    def minorRegex = /^$major\.${minor}\..*/
    return !((candidate ==~ majorRegex) && (!matchMinor || (candidate ==~ minorRegex)))
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        // only patch updates
        isNonStable(it.candidate.version) || isNotSameMajorMinor(it.currentVersion, it.candidate.version, true)
    }
}

tasks.register('dependencyUpdatesMinor', DependencyUpdatesTask) {
    rejectVersionIf {
        // only minor updates
        isNonStable(it.candidate.version) || isNotSameMajorMinor(it.currentVersion, it.candidate.version, false)
    }
}

tasks.register('dependencyUpdatesMajor', DependencyUpdatesTask) {
    rejectVersionIf {
        // all updates including major updates
        isNonStable(it.candidate.version)
    }
}

tasks.withType(DependencyUpdatesTask).configureEach {
    // default settings
    revision = 'milestone'
    gradleReleaseChannel = "current"
    checkConstraints = true
    checkBuildEnvironmentConstraints = true
    outputFormatter = 'json,plain'
    outputDir = 'build/reports'
    reportfileName = 'dependencyUpdates'
}
